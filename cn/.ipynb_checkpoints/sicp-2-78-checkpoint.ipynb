{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "0454da55",
   "metadata": {},
   "source": [
    "## SICP 习题 （2.78）解题总结： 结合Scheme的系统数字体系"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "51688950",
   "metadata": {},
   "source": [
    "SICP 习题 2.78 希望我们修改 2.77的代码，让2.77的代码可以支持Scheme里的常规数字。\n",
    "\n",
    "在2.77里，如果我们希望操作基本的Scheme数字，我们需要使用：\n",
    "\n",
    "    (add (make-scheme-number 2) (make-scheme-number 3))\n",
    "    \n",
    "这样的代码，需要对数字进行打包，打包成一个叫做scheme-number的类型。\n",
    "\n",
    "题目希望我们可以直接支持常规数字的操作，像下面这样：\n",
    "\n",
    "    (add 2 3)\n",
    "    \n",
    "上面两段代码我们在2.77的彩蛋里都测试过，像`(add 2 3)`这样的代码时会报错的。\n",
    "\n",
    "题目还非常贴心地提醒了我们需要修改的函数有哪些， 分别是attach-tag, type-tag, contents。\n",
    "\n",
    "下面就直接修改代码，这里用到的方法就是使用(number?)对操作数进行判断，如果是一个常规数字，我们就做特殊处理。\n",
    "\n",
    "在attach-tag的时候，对常规数字直接就不打tag； 在判断操作数的type-tag的时候，如果是常规数字就返回'scheme-number，告诉调用者这个其实是一个scheme-number； 在获取contents的时候，如果是常规数字就直接返回数字："
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 13,
   "id": "4e768591",
   "metadata": {},
   "outputs": [],
   "source": [
    "(define (attach-tag type-tag contents)\n",
    "  (cond ((number? contents) contents)\n",
    "        (else (cons type-tag contents))))\n",
    "\n",
    "\n",
    "(define (type-tag datum)\n",
    "  (if (pair? datum)\n",
    "      (car datum)\n",
    "      (if (number? datum)\n",
    "\t  'scheme-number\n",
    "\t  (error \"Bad tagged type -- TYPE \" datum))))\n",
    "\n",
    "(define (contents datum)\n",
    "  (if (pair? datum)\n",
    "      (cdr datum)\n",
    "      (if (number? datum)\n",
    "\t  datum\n",
    "\t  (error \"Bad tagged datum -- CONTENTS\" datum))))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "22cf65e8",
   "metadata": {},
   "source": [
    "剩下就是一样的代码了："
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 14,
   "id": "3708ecf1",
   "metadata": {},
   "outputs": [],
   "source": [
    "(import 'math)\n",
    "\n",
    "(python-exec \"\n",
    "def cos(input_number):\n",
    "    return math.cos(input_number)\n",
    "             \")\n",
    "\n",
    "(python-exec \"\n",
    "def sin(input_number):\n",
    "    return math.sin(input_number)\n",
    "             \")\n",
    "\n",
    "(python-exec \"\n",
    "def atan(input_number):\n",
    "    return math.atan(input_number)\n",
    "             \")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 15,
   "id": "a8ed1f07",
   "metadata": {},
   "outputs": [],
   "source": [
    "(define (assoc key records)\n",
    "  (cond ((null? records) #f)\n",
    "\t((equal? key (caar records)) (car records))\n",
    "\t(else (assoc key (cdr records)))))\n",
    "\n",
    "(define (make-table)\n",
    "  (let ((local-table (list '*table*)))\n",
    "    (define (lookup key-1 key-2)\n",
    "      (let ((subtable (assoc key-1 (cdr local-table))))\n",
    "\t(if subtable\n",
    "\t    (let ((record (assoc key-2 (cdr subtable))))\n",
    "\t      (if record\n",
    "\t\t  (cdr record)\n",
    "\t\t  (begin\n",
    "\t\t    (display \"record not found: \") (display key-2) (newline)\n",
    "\t\t    #f\n",
    "\t\t    )))\n",
    "\t    (begin \n",
    "\t      (display \"subtable not found: \") (display key-1)(newline)\n",
    "\t      #f))))\n",
    "    (define (insert! key-1 key-2 value)\n",
    "      (let ((subtable (assoc key-1 (cdr local-table))))\n",
    "\t(if subtable\n",
    "\t    (let ((record (assoc key-2 (cdr subtable))))\n",
    "\t      (if record\n",
    "\t\t  (set-cdr! record value)\n",
    "\t\t  (set-cdr! subtable\n",
    "\t\t\t    (cons (cons key-2 value)\n",
    "\t\t\t\t  (cdr subtable)))))\n",
    "\t    (set-cdr! local-table\n",
    "\t\t      (cons (list key-1\n",
    "\t\t\t\t  (cons key-2 value))\n",
    "\t\t\t    (cdr local-table)))))\n",
    "      'ok)\n",
    "    (define (dispatch m)\n",
    "      (cond ((eq? m 'lookup-proc) lookup)\n",
    "\t    ((eq? m 'insert-proc!) insert!)\n",
    "\t    (else (error \"Unknown operation -- TABLE\" m))))\n",
    "    dispatch))\n",
    "\n",
    "\n",
    "(define operation-table (make-table))\n",
    "\n",
    "(define get (operation-table 'lookup-proc))\n",
    "\n",
    "(define put (operation-table 'insert-proc!))\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "(define (add x y) (apply-generic 'add x y))\n",
    "\n",
    "(define (sub x y) (apply-generic 'sub x y))\n",
    "\n",
    "(define (mul x y) (apply-generic 'mul x y))\n",
    "\n",
    "(define (div x y) (apply-generic 'div x y))\n",
    "\n",
    "\n",
    "\n",
    "(define (install-scheme-number-package)\n",
    "  (define (tag x)\n",
    "    (attach-tag 'scheme-number x))\n",
    "  (put 'add '(scheme-number scheme-number)\n",
    "       (lambda (x y) (tag (+ x y))))\n",
    "  (put 'sub '(scheme-number scheme-number)\n",
    "       (lambda (x y) (tag (- x y))))\n",
    "  (put 'mul '(scheme-number scheme-number)\n",
    "       (lambda (x y) (tag (* x y))))\n",
    "  (put 'div '(scheme-number scheme-number)\n",
    "       (lambda (x y) (tag (/ x y))))\n",
    "  (put 'make 'scheme-number\n",
    "       (lambda (x) (tag x)))\n",
    "  'done)\n",
    "\n",
    "(define (make-scheme-number n)\n",
    "  ((get 'make 'scheme-number) n))\n",
    "\n",
    "(define (install-rational-package)\n",
    "  (define (numer x) (car x))\n",
    "  (define (denom x) (cdr x))\n",
    "  (define (make-rat n d)\n",
    "    (let ((g (gcd n d)))\n",
    "      (cons (/ n g) (/ d g))))\n",
    "  (define (add-rat x y)\n",
    "    (make-rat (+ (* (numer x) (denom y))\n",
    "\t\t (* (numer y) (denom x)))\n",
    "\t      (* (denom x) (denom y))))\n",
    "  (define (sub-rat x y)\n",
    "    (make-rat (- (* (numer x) (denom y))\n",
    "\t\t (* (numer y) (denom x)))\n",
    "\t      (* (denom x) (denom y))))\n",
    "  (define (mul-rat x y)\n",
    "    (make-rat (* (numer x) (numer y))\n",
    "\t      (* (denom x) (denom y))))\n",
    "  (define (div-rat x y)\n",
    "    (make-rat (* (numer x) (denom y))\n",
    "\t      (* (denom x) (numer y))))\n",
    "\n",
    "  (define (tag x) (attach-tag 'rational x))\n",
    "  (put 'add '(rational rational)\n",
    "       (lambda (x y) (tag (add-rat x y))))\n",
    "  (put 'sub '(rational rational)\n",
    "       (lambda (x y) (tag (sub-rat x y))))\n",
    "  (put 'mul '(rational rational)\n",
    "       (lambda (x y) (tag (mul-rat x y))))\n",
    "  (put 'div '(rational rational)\n",
    "       (lambda (x y) (tag (div-rat x y))))\n",
    "\n",
    "  (put 'make 'rational\n",
    "       (lambda (n d) (tag (make-rat n d))))\n",
    "  'done)\n",
    "\n",
    "(define (make-rational n d)\n",
    "  ((get 'make 'rational) n d))\n",
    "\n",
    "\n",
    "(define (apply-generic op . args)\n",
    "  (let ((type-tags (map type-tag args)))\n",
    "    (let ((proc (get op type-tags)))\n",
    "      (if proc\n",
    "\t  (apply proc (map contents args))\n",
    "\t  (error \n",
    "\t   \"No method for these types -- APPLY-GENERIC\"\n",
    "\t   (list op type-tags))))))\n",
    "\n",
    "(define (real-part z) (apply-generic 'real-part z))\n",
    "(define (imag-part z) (apply-generic 'imag-part z))\n",
    "(define (magnitude z) (apply-generic 'magnitude z))\n",
    "(define (angle z) (apply-generic 'angle z))\n",
    "\n",
    "\n",
    "(define (install-rectangular-package)\n",
    "  (define (real-part z) (car z))\n",
    "  (define (imag-part z) (cdr z))\n",
    "  (define (make-from-real-imag x y) (cons x y))\n",
    "  (define (magnitude z)\n",
    "    (sqrt (+ (square (real-part z))\n",
    "\t     (square (imag-part z)))))\n",
    "  (define (angle z)\n",
    "    (atan (/ (imag-part z) (real-part z))))\n",
    "  (define (make-from-mag-ang r a)\n",
    "    (cons (* r (cos a)) (* r (sin a))))\n",
    "\n",
    "  (define (tag x) (attach-tag 'rectangular x))\n",
    "  (put 'real-part '(rectangular) real-part)\n",
    "  (put 'imag-part '(rectangular) imag-part)\n",
    "  (put 'magnitude '(rectangular) magnitude)\n",
    "  (put 'angle '(rectangular) angle)\n",
    "  (put 'make-from-real-imag 'rectangular\n",
    "       (lambda (x y) (tag (make-from-real-imag x y))))\n",
    "  (put 'make-from-mag-ang 'rectangular\n",
    "       (lambda (r a) (tag (make-from-mag-ang r a))))\n",
    "  'done)\n",
    "\n",
    "(define (install-polar-package)\n",
    "  (define (magnitude z) (car z))\n",
    "  (define (angle z) (cdr z))\n",
    "  (define (make-from-mag-ang r a) (cons r a))\n",
    "  (define (real-part z)\n",
    "    (* (magnitude z) (cos (angle z))))\n",
    "  (define (imag-part z)\n",
    "    (* (magnitude z) (sing (angle z))))\n",
    "  (define (make-from-real-imag x y)\n",
    "    (cons (sqrt (+ (square x) (square y)))\n",
    "\t  (atan (/ y x))))\n",
    "  (define (tag x) (attach-tag 'polar x))\n",
    "  (put 'real-part '(polar) real-part)\n",
    "  (put 'imag-part '(polar) imag-part)\n",
    "  (put 'magnitude '(polar) magnitude)\n",
    "  (put 'angle '(polar) angle)\n",
    "  (put 'make-from-real-imag 'polar\n",
    "       (lambda (x y) (tag (make-from-real-imag x y))))\n",
    "  (put 'make-from-mag-ang 'polar\n",
    "       (lambda (r a) (tag (make-from-mag-ang r a))))\n",
    "  'done)\n",
    "\n",
    "(define (install-complex-package)\n",
    "  (define (make-from-real-imag x y)\n",
    "    ((get 'make-from-real-imag 'rectangular) x y))\n",
    "  (define (make-from-mag-ang r a)\n",
    "    ((get 'make-from-mag-ang 'polar) r a))\n",
    "\n",
    "  (define (add-complex z1 z2)\n",
    "    (make-from-real-imag (+ (real-part z1) (real-part z2))\n",
    "\t\t\t (+ (imag-part z1) (imag-part z2))))\n",
    "\n",
    "  (define (sub-complex z1 z2)\n",
    "    (make-from-real-imag (- (real-part z1) (real-part z2))\n",
    "\t\t\t (- (imag-part z1) (imag-part z2))))\n",
    "\n",
    "  (define (mul-complex z1 z2)\n",
    "    (make-from-mag-ang (* (magnitude z1) (magnitude z2))\n",
    "\t\t       (+ (angle z1) (angle z2))))\n",
    "\n",
    "  (define (div-complex z1 z2)\n",
    "    (make-from-mag-ang (/ (magnitude z1) (magnitude z2))\n",
    "\t\t       (- (angle z1 ) (angle z2))))\n",
    "\n",
    "  (define (tag z) (attach-tag 'complex z))\n",
    "\n",
    "  ; the following lines are added for this execise:\n",
    "  (put 'real-part '(complex) real-part)\n",
    "  (put 'imag-part '(complex) imag-part)\n",
    "  (put 'magnitude '(complex) magnitude)\n",
    "  (put 'angle '(complex) angle)\n",
    "  ;end of the execise\n",
    "\n",
    "  (put 'add '(complex complex)\n",
    "       (lambda (z1 z2) (tag (add-complex z1 z2))))\n",
    "  (put 'sub '(complex complex)\n",
    "       (lambda (z1 z2) (tag (sub-complex z1 z2))))\n",
    "  (put 'mul '(complex complex)\n",
    "       (lambda (z1 z2) (tag (mul-complex z1 z2))))\n",
    "  (put 'div '(complex complex)\n",
    "       (lambda (z1 z2) (tag (div-complex z1 z2))))\n",
    "  (put 'make-from-real-imag 'complex\n",
    "       (lambda (x y) (tag (make-from-real-imag x y))))\n",
    "  (put 'make-from-mag-ang 'complex\n",
    "       (lambda (r a) (tag (make-from-mag-ang r a))))\n",
    "  'done)\n",
    "\n",
    "(define (make-complex-from-real-imag x y)\n",
    "  ((get 'make-from-real-imag 'complex) x y))\n",
    "(define (make-complex-from-mag-ang r a)\n",
    "  ((get 'make-from-mag-ang 'complex) r a))\n",
    "\n",
    "(define (display-complex complex)\n",
    " (display \"--------------------\") (newline)\n",
    "  (display \"real part:\") (display (real-part complex)) (newline)\n",
    "  (display \"imag part:\") (display (imag-part complex)) (newline)\n",
    "  (display \"magnitude:\") (display (magnitude complex)) (newline)\n",
    "  (display \"angle    :\") (display (angle complex))(newline)\n",
    "  (display \"--------------------\") (newline)\n",
    ")\n",
    "\n",
    "(define (start-test-2-78)\n",
    "  (install-scheme-number-package)\n",
    "  \n",
    "  (add 2 3)\n",
    "  )\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 16,
   "id": "3e21f5dd",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "5"
      ]
     },
     "execution_count": 16,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "(start-test-2-78)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 17,
   "id": "25f216a9",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "5"
      ]
     },
     "execution_count": 17,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "(div 10 2)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b06c1510",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Calysto Scheme 3",
   "language": "scheme",
   "name": "calysto_scheme"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "scheme"
   },
   "mimetype": "text/x-scheme",
   "name": "scheme",
   "pygments_lexer": "scheme"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
